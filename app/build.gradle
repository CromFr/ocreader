/*
 * Copyright (C) 2015-2016 Daniel Schaal <daniel@schaal.email>
 *
 * This file is part of OCReader.
 *
 * OCReader is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * OCReader is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with OCReader.  If not, see <http://www.gnu.org/licenses/>.
 *
 */

apply plugin: 'com.android.application'
apply plugin: 'realm-android'
apply plugin: 'com.jakewharton.hugo'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.2"

    dataBinding.enabled = true

    defaultConfig {
        applicationId "email.schaal.ocreader"
        minSdkVersion 17
        targetSdkVersion 27
        versionCode 41
        versionName "0.38"

        archivesBaseName="${getApplicationId()}_${getVersionCode()}"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
        disable 'MissingTranslation'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

ext {
    versions = [
            supportLib: '27.0.2',
            okhttp: '3.9.1',
            retrofit: '2.3.0',
            glide: '4.4.0'
    ]
}

dependencies {
    implementation "com.android.support:support-compat:${versions.supportLib}"
    implementation "com.android.support:support-core-ui:${versions.supportLib}"

    implementation "com.android.support:appcompat-v7:${versions.supportLib}"
    implementation "com.android.support:cardview-v7:${versions.supportLib}"
    implementation "com.android.support:recyclerview-v7:${versions.supportLib}"
    implementation "com.android.support:design:${versions.supportLib}"
    implementation "com.android.support:palette-v7:${versions.supportLib}"

    implementation "com.android.support:support-v13:${versions.supportLib}"

    implementation('com.mikepenz:materialdrawer:6.0.2@aar') {
        transitive = true
    }

    implementation('com.mikepenz:aboutlibraries:6.0.1@aar') {
        transitive = true
    }

    implementation 'io.realm:android-adapters:2.1.0'

    implementation 'org.jsoup:jsoup:1.11.2'

    implementation "com.squareup.okhttp3:okhttp:${versions.okhttp}"

    implementation "com.squareup.retrofit2:retrofit:${versions.retrofit}"
    implementation "com.squareup.retrofit2:converter-moshi:${versions.retrofit}"

    implementation("com.github.bumptech.glide:glide:${versions.glide}@aar") {
        transitive = true
    }
    implementation "com.github.bumptech.glide:okhttp3-integration:${versions.glide}@aar"
    implementation "com.github.bumptech.glide:recyclerview-integration:${versions.glide}@aar"

    annotationProcessor "com.github.bumptech.glide:compiler:${versions.glide}"

    implementation 'com.github.zafarkhaja:java-semver:0.9.0'

    releaseImplementation 'ch.acra:acra:4.9.2'

    implementation('com.vdurmont:emoji-java:4.0.0', {
        exclude group: 'org.json', module: 'json'
    })

    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    })

    androidTestImplementation "com.squareup.okhttp3:mockwebserver:${versions.okhttp}"

    testImplementation "org.robolectric:robolectric:3.5.1"
    testImplementation 'junit:junit:4.12'
}
